/*
 * Copyright 2013-2016 Urs Wolfer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.jetbrains.intellij' version '0.1.10'
}

apply plugin: 'java'

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile ('com.google.inject:guice:4.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile ('com.google.inject.extensions:guice-multibindings:4.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile ('com.urswolfer.gerrit.client.rest:gerrit-rest-java-client:0.8.8') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'org.javassist:javassist:3.19.0-GA'

    testCompile 'org.testng:testng:6.8.7'
    testCompile 'org.easymock:easymock:3.2'
}

intellij {
    version ideaVersion
    pluginName 'gerrit-intellij-plugin'
    updateSinceUntilBuild false
    downloadSources Boolean.valueOf(downloadIdeaSources)
    plugins 'git4idea'

    publish {
        channel ijPluginRepoChannel
        username System.getenv('IJ_REPO_USERNAME')
        password System.getenv('IJ_REPO_PASSWORD')
    }
}

test {
    useTestNG()
    exclude 'com/urswolfer/intellij/plugin/gerrit/rest/GerritUtilTest.*' // fails with java.lang.VerifyError at GerritUtilTest.java:34
}
