/*
 * Copyright 2013-2016 Urs Wolfer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'org.jetbrains.intellij' version '0.7.2'
}

apply plugin: 'java'

targetCompatibility = javaVersion
sourceCompatibility = javaVersion

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation ('com.google.inject:guice:4.1') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation ('com.google.inject.extensions:guice-multibindings:4.1.0') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation ('com.urswolfer.gerrit.client.rest:gerrit-rest-java-client:0.9.3') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation 'org.javassist:javassist:3.27.0-GA'

    testImplementation 'org.testng:testng:7.3.0'
    testImplementation 'org.easymock:easymock:4.2'
}

intellij {
    version ideaVersion
    pluginName 'gerrit-intellij-plugin'
    updateSinceUntilBuild false
    downloadSources Boolean.valueOf(downloadIdeaSources)
    plugins 'git4idea'

    publishPlugin {
        channels ijPluginRepoChannel
        token System.getenv('IJ_REPO_TOKEN')
    }
}

test {
    useTestNG()
    exclude 'com/urswolfer/intellij/plugin/gerrit/rest/GerritUtilTest.*' // fails with java.lang.VerifyError at GerritUtilTest.java:34
}
